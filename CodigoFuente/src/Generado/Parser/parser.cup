package Generado.Parser;
import java_cup.runtime.*;
import java.util.ArrayList;
parser code
{:
  public boolean syntaxErrors;

  ArrayList<String> errores;
  PilaSemantica pila;
  TablaSimbolos tabla;

  public Analizador(java_cup.runtime.Scanner s, boolean type) {
    super(s);
    errores = new ArrayList<String>();
    pila = new PilaSemantica();
    tabla = new TablaSimbolos();
  }
  
  public void addError(String error){
    String error2 = errores.get(errores.size()-1);
    error2 = error + error2;
    errores.set(errores.size()-1, error2);
    /*ProyectoCompi.PoyectoCompi.consola.impirmir(error);*/
  }

   public ArrayList<String> retornarLista(){
        return errores;
   }
  
   public PilaSemantica retornarPila(){
       return pila;
   }
   public TablaSimbolos retornarTabla(){
       return tabla;
   }

  public void syntax_error(Symbol s){
        String value = "";
    if(s.value == null){
        value = "Se llego al final del archivo sin cerrar un bloque";
    }else
        value = s.value.toString();

    errores.add(" En el token: " + value + " En la linea: " + s.left + " En la columna: " + s.right);
    System.out.println("Error en la linea : (" + s.left + ") " + s.value);
   
  }
/*public void unrecovered_syntax_error(Symbol s){
    System.out.println("Error crítico en la linea : " + s.value);
  }*/
protected int error_sync_size(){
    return 1;
}

:}
action code
{:
:}

/*----------------------------------------------------------------------*/
/* Declaracion de terminales */
terminal palabraReservada, INT,FLOAT,CHAR,STRING,opAritmeticos,opComGeneral,opComEspecifico,opAsignaciones,opDelimitadores,opLogicos,ERROR,END;

terminal intReservado,clas,def,ffalse,ttrue,iif,eelse,elif;
terminal var;
terminal identificador;

terminal ffor,in,range,wwhile,pprint,iinput,ttry,eexcept,ffinally,bbreak,ccontinue,rreturn;

/* operadores */
terminal opSuma, opResta, opMultiplicacion, opDivision, opDivisionE,opModulo,opPotencia;
terminal opAnd, opOr, opNot,opShiftR,opShiftL,opAndBits,opExclusiveOr,opComp,opOrBits;

/*Delimitadores*/
terminal dosPuntos,puntoComa,parenAbierto,parenCerrado,coma,cuadradoCerrado,cuadradoAbierto,punto;

/*--------------------------------------------------------------------------*/

/* Declaracion de no terminales */
non terminal Programa, CodigoPrincipal;
non terminal Variable;
non terminal Funcional,DefFuncion,DefFunciones,DefFunciones2,DefFuncionesFact,DefParametrosFact,DefParametros,Funcion,Parametro,Parametros,DefVariables,DefVariablesFact;
non terminal Variables,VariableRec;
non terminal Bloque,Bloques,BloqueNuevo, DecIf,If, Else, Elif, ForFact, For, Range, While,Var,OpBits;

/* Zona OOP */
non terminal OOP, HeaderOOP, VariablesOOP, Clase,BloqueOOP, cuerpoOOP,FuncionesOOP,PuntoComaOOP, VariableOOP, FuncionesOOP2;

/* Operadores*/
non terminal PuntoComa,DosPuntos;

/**Errores**/
non terminal errHeaderOOP, errDecVariables, errPuntoYComa, errClase,errExpresion, errIf, errAsignacion,errExpresionB;
non terminal errInput, errPrint,errDecFuncion,errParametro1,errParametro,errBloque,errIn,errRange,errFor,errElse,errDosPuntos;
non terminal errTry, errExcept, errExcept2, errFinally,errInt,errParen,errMalUbicacion;

/* Semantico */
non terminal NombreFuncion;


/* Común para todos */
non terminal Expresion,ExpresionB,Literal,Lista,ElementosLista,MasElementosLista, Expresiones,ExpresionGeneral,ExpresionesB,ExpresionOP,ExpresionBol,ExpresionUnario,ExpresionBinaria ;
non terminal ElementosLista2, MasElementosLista2;
non terminal OpAritmeticos, OpLogicos, OpBinario, OpUnario,OpBinarioB;
non terminal Asignacion,Asignaciones;
non terminal FuncionPredefinida,FuncionesPredefinidas, Input, Print,BloquePuntoComa;
non terminal CualquierCosas,CualquierCosa;
non terminal TryExceptFinally, Try , Except , Finally,IntFuncion;

/* Prueba break,continue */
non terminal BloquePuntoComaEspecial,CualquierCosaEspecial,CualquierCosasEspecial,errEspecial,IfEspecial,ElseEspecial,ElifEspecial,TryEspecial,ExceptEspecial,FinallyEspecial;

/*------------------------------Precendencias--------------------------------------*/


precedence left opNot,opOr,opAnd;
precedence left opComGeneral,opComEspecifico;
precedence left opAsignaciones;
precedence left opSuma, opResta;
precedence left opMultiplicacion, opDivisionE, opModulo, opDivision;
precedence left opPotencia,INT;
precedence left parenAbierto, parenCerrado;
precedence left eelse,elif,iif,wwhile,ffor;
precedence left dosPuntos,puntoComa,def;
precedence left pprint, iinput;



/*--------------------- Configuraciones --------------------------------------- */

/* inicio de gramatica */
start with Programa ;


/*----------------- Producciones ----------------------------------------------------- */
Programa::= Funcional {: ProyectoCompi.PoyectoCompi.consola.impirmir("Parseo realizado exitosamente. De tipo Funcional."); :}
| OOP                 {: ProyectoCompi.PoyectoCompi.consola.impirmir("Parseo realizado exitosamente. De tipo OOP."); :}
| error               {: ProyectoCompi.PoyectoCompi.consola.impirmir("Error por final de archivo. (Simbolos extra?)");:}
;

Funcional ::= DefFunciones CodigoPrincipal;


NombreFuncion ::= identificador:v {: pila.recuerdaFuncion(v,vleft,vright,tabla); :} ;

DefFunciones ::= DefFuncion DefFunciones | ;
DefFuncion ::= def NombreFuncion parenAbierto DefParametrosFact dosPuntos {: pila.ambitoActual = "LOCAL"; :} Variables CualquierCosas puntoComa {: tabla.insertarFuncion(pila); :} 
| def NombreFuncion parenAbierto DefParametrosFact dosPuntos Variables CualquierCosas errPuntoYComa
| def errDecFuncion dosPuntos Variables CualquierCosas puntoComa
| def NombreFuncion parenAbierto DefParametrosFact errDecFuncion dosPuntos
| def errDecFuncion puntoComa  /* si no ponen los dos puntos no lee todo el bloque siguiente */
| def parenAbierto errDecFuncion puntoComa
| def errPuntoYComa DefFuncion
| errDecFuncion DefFuncion
| errDecFuncion puntoComa
;



DefParametrosFact ::= Var identificador:v {: pila.ambitoActual = "PARAMETRO"; pila.recuerdaId(v,"VARIABLE",vleft,vright);  tabla.insertarVariables(pila); :} DefParametros
| var errParametro1 DefParametros| identificador errParametro1 DefParametros 
| parenCerrado; 

DefParametros::= coma Var identificador:v {: pila.recuerdaId(v,"VARIABLE",vleft,vright);  tabla.insertarVariables(pila); :} DefParametros 
| parenCerrado | errParametro1 ; 


/*-----------------------------------------------------------------------------------------*/

Var ::= var:v {: pila.recuerdaTipo(v,vleft,vright); :}  | intReservado:v {: pila.recuerdaTipo(v,vleft,vright); :} ;
/* creo que habra que separar las variables */

Variable::= Var DefVariablesFact {: tabla.insertarVariables(pila); :}| Var errDecVariables | errDecVariables identificador ;
Variables ::= Variables Variable| ;

DefVariablesFact ::= identificador:v {: pila.recuerdaId(v,"VARIABLE",vleft,vright); :} DefVariables | errDecVariables DefVariables; 
DefVariables::= coma identificador:v {: pila.recuerdaId(v,"VARIABLE",vleft,vright); :}  DefVariables 
|  errDecVariables identificador:v DefVariables | errDecVariables DefVariables |; 


/*---------------------------------------------------------------------------------------*/
Funcion::= identificador:v {:pila.registrarFuncion(v, vleft, vright, tabla); :} parenAbierto Parametros
| identificador:v {:pila.registrarFuncion(v, vleft, vright, tabla); :} errParen parenCerrado
;

Parametros ::= ExpresionGeneral Parametro {:pila.evalFuncion(tabla); :} | parenCerrado {:pila.evalFuncion(tabla); :} 
|errParen
;
Parametro ::= coma ExpresionGeneral Parametro | parenCerrado | errParametro parenCerrado ;

If ::= iif:v {: pila.startIf(v,vleft,vright); :} ExpresionB {: pila.evalExpresion(tabla,null); pila.finExpresion(); pila.testIf(); :} dosPuntos BloquePuntoComa Elif {: pila.endIf(tabla); :}
| iif:v {: pila.startIf(v,vleft,vright); :} ExpresionB {: pila.evalExpresion(tabla,null); pila.finExpresion(); pila.testIf(); :} error:e {: parser.addError("Hay un error en la declaracion del If" + 
" (revisar dos puntos)."); :} dosPuntos BloquePuntoComa Elif {: pila.endIf(tabla); :}
| iif:v {: pila.startIf(v,vleft,vright); :} errExpresionB 
 dosPuntos BloquePuntoComa Elif {: pila.endIf(tabla); :}
;

IfEspecial ::= iif:v {: pila.startIf(v,vleft,vright); :} ExpresionB {: pila.evalExpresion(tabla,null); pila.finExpresion(); pila.testIf(); :} dosPuntos BloquePuntoComaEspecial ElifEspecial
| iif:v {: pila.startIf(v,vleft,vright); :} ExpresionB{: pila.evalExpresion(tabla,null); pila.finExpresion(); pila.testIf(); :} error:e {: parser.addError("Hay un error en la declaracion del If" + 
" (revisar dos puntos)."); :}
 dosPuntos BloquePuntoComaEspecial ElifEspecial
| iif:v {: pila.startIf(v,vleft,vright); :} errExpresionB 
 dosPuntos BloquePuntoComaEspecial ElifEspecial
;

Elif ::= elif ExpresionB dosPuntos BloquePuntoComa Elif 
| elif error:e {: parser.addError("Hay un error en la expresion del If" + 
" (revisar dos puntos)."); :}
dosPuntos BloquePuntoComa Elif
| Else; 

ElifEspecial ::= elif ExpresionB dosPuntos BloquePuntoComaEspecial ElifEspecial 
| elif error:e {: parser.addError("Hay un error en la expresion del If" + 
" (revisar dos puntos)."); :}
dosPuntos BloquePuntoComaEspecial ElifEspecial
| ElseEspecial; 

Else::= eelse:v {: pila.startElse(); :} dosPuntos BloquePuntoComaEspecial
| eelse errElse dosPuntos BloquePuntoComaEspecial
|
;

ElseEspecial::= eelse {: pila.startElse(); :} dosPuntos BloquePuntoComaEspecial
| eelse errElse dosPuntos BloquePuntoComaEspecial
|
;

ForFact ::= ffor identificador in For dosPuntos BloquePuntoComaEspecial
| ffor errFor in For dosPuntos BloquePuntoComaEspecial
| ffor identificador in For errFor dosPuntos BloquePuntoComaEspecial
| ffor identificador errIn For dosPuntos BloquePuntoComaEspecial ;

For ::= STRING | identificador | Range | errFor ;
Range ::= range parenAbierto INT coma INT parenCerrado | range parenAbierto INT parenCerrado | range errRange parenCerrado
| range parenAbierto INT error:e  {: parser.addError("Erro falta parentesis range o puntoComa."); :} 
| range parenAbierto INT coma INT error:e {: parser.addError("Erro falta parentesis range o dos puntos."); :}
| range errFor;

While ::= wwhile ExpresionB dosPuntos BloquePuntoComaEspecial Else
| wwhile parenAbierto parenCerrado dosPuntos BloquePuntoComaEspecial Else
| wwhile error:e dosPuntos {: parser.addError("Hay un error en la expresionn del While" + 
"."); :}  BloquePuntoComaEspecial Else
| wwhile ExpresionB error:e dosPuntos {: parser.addError("Hay un error en la declaracion del While" + 
"."); :}  BloquePuntoComaEspecial Else    
; 

BloquePuntoComa ::= CualquierCosas puntoComa | errPuntoYComa puntoComa ;
BloquePuntoComaEspecial ::= CualquierCosasEspecial puntoComa | errPuntoYComa puntoComa ;

BloqueNuevo ::= If BloqueNuevo | ForFact BloqueNuevo | While BloqueNuevo 
| Try BloqueNuevo |  errBloque puntoComa ; 

Bloques::= Bloque Bloques | Bloque;
Bloque ::= ForFact Bloque | While Bloque | If Bloque | Try Bloque;


Try::= ttry dosPuntos CualquierCosas puntoComa Except
| ttry errTry  BloquePuntoComa Except
| ttry dosPuntos CualquierCosas errExcept puntoComa
;

TryEspecial::= ttry dosPuntos CualquierCosasEspecial puntoComa ExceptEspecial
| ttry errTry  BloquePuntoComaEspecial Except
| ttry dosPuntos CualquierCosasEspecial errExcept puntoComa
;


Except::= eexcept identificador dosPuntos BloquePuntoComa Finally
| eexcept error:e {: parser.addError("Hay un error con la definición del except" + 
" (revisar dos puntos o identificador)." ); :}
dosPuntos BloquePuntoComa Finally
| errExcept2
;

ExceptEspecial::= eexcept identificador dosPuntos BloquePuntoComaEspecial FinallyEspecial
| eexcept error:e {: parser.addError("Hay un error con la definición del except" + 
" (revisar dos puntos o identificador)." ); :}
dosPuntos BloquePuntoComaEspecial FinallyEspecial
| errExcept2
;

Finally::= ffinally dosPuntos BloquePuntoComa
| ffinally errFinally dosPuntos BloquePuntoComa
|
;

FinallyEspecial::= ffinally dosPuntos BloquePuntoComaEspecial
| ffinally errFinally dosPuntos BloquePuntoComaEspecial
|
;


Literal ::= INT:v {:pila.registrarLiteral(v, "int" ,vleft, vright); System.out.println(v); RESULT = new Nodo(null,null, v); :}
|FLOAT:v          {:pila.registrarLiteral(v, "float" ,vleft, vright); :}
|CHAR:v           {:pila.registrarLiteral(v, "char" ,vleft, vright); :}
|STRING:v         {:pila.registrarLiteral(v, "string" ,vleft, vright); :}
;


Lista::= cuadradoAbierto ElementosLista cuadradoCerrado:v {:pila.registrarLiteral("lista", "LIST" ,vleft, vright); pila.contadorExp++; :};
ElementosLista::= ExpresionGeneral {: pila.finExpresion(); :} MasElementosLista| ;
MasElementosLista::= coma ExpresionGeneral {: pila.finExpresion(); :} MasElementosLista | ;
Expresion ::= OpAritmeticos:v {: RESULT = v;  :}
| ExpresionB
| ExpresionUnario 
| ExpresionBinaria ;  
Expresion ::= Literal:v {: pila.contadorExp+=1; RESULT = v; :}|Lista|identificador:v {:pila.registrarId(v, vleft, vright, tabla); pila.contadorExp+=1; :}
        | parenAbierto Expresion parenCerrado
        | errExpresion /*solo un error general para expresiones */
        | parenAbierto errParen
        | errParen parenCerrado;
ExpresionOP ::= Expresion:e1 OpAritmeticos:v Expresion:e2 {: RESULT = new Nodo(e1,e2,v);:};
ExpresionGeneral ::= Expresion:v {: pila.evalExpresion(tabla,v);   :};

ExpresionBol::= Expresion OpBinarioB Expresion ;
ExpresionUnario ::= OpUnario Expresion;
Expresion ::= identificador punto Funcion;
Expresion ::= ExpresionB;
Expresion ::= FuncionPredefinida;
ExpresionBinaria ::= Expresion OpBits Expresion;  

ExpresionB ::= ffalse:v {:pila.registrarLiteral(v, "boolean" ,vleft, vright); pila.contadorExp+=1; :}
| ttrue:v               {:pila.registrarLiteral(v, "boolean" ,vleft, vright); pila.contadorExp+=1;:}
| Funcion {: pila.contadorExp++; :}
| opNot:v {:pila.registrarOperador(v, "NOT" ,vleft, vright); :} Expresion  
| ExpresionBol 
| parenAbierto ExpresionB parenCerrado ;

Expresion ::= identificador:c {:pila.registrarId(c, cleft, cright, tabla); pila.contadorExp+=1; :} 
opAsignaciones:v {:pila.registrarOperador(v, "ASIGNACION" ,vleft, vright); :} Expresion;


Expresiones ::= Expresiones Expresion | ;
ExpresionesB ::= ExpresionesB ExpresionB | ;
Asignaciones ::= Asignaciones Asignacion | ;
FuncionesPredefinidas::= FuncionesPredefinidas FuncionPredefinida | ;

FuncionPredefinida::= Input | Print | IntFuncion ;
IntFuncion ::= intReservado:v {:pila.registrarId(v, vleft, vright, tabla); pila.contadorExp+=1; :} parenAbierto FuncionPredefinida parenCerrado 
| intReservado:v {:pila.registrarId(v, vleft, vright, tabla); pila.contadorExp+=1; :} parenAbierto Funcion parenCerrado
|intReservado:v {:pila.registrarId(v, vleft, vright, tabla); pila.contadorExp+=1; :} parenAbierto STRING parenCerrado 
| errInput;

Input ::= iinput:v {:pila.registrarId(v, vleft, vright, tabla); pila.contadorExp+=1; :} parenAbierto Expresion parenCerrado 
| iinput:v {:pila.registrarId(v, vleft, vright, tabla); pila.contadorExp+=1; :} parenAbierto parenCerrado
| iinput errInput 
| errInput;
Print ::= pprint:v {:pila.registrarId(v, vleft, vright, tabla); pila.contadorExp+=1; :} parenAbierto ElementosLista2 parenCerrado 
| errPrint
| pprint parenAbierto errPrint parenCerrado;

ElementosLista2::= Expresion MasElementosLista2;
MasElementosLista2::= coma Expresion MasElementosLista2| ;

CodigoPrincipal::= {: pila.ambitoActual = "GLOBAL"; :} Variables CualquierCosas;
CualquierCosas::= CualquierCosas CualquierCosa | ;
CualquierCosasEspecial::= CualquierCosasEspecial CualquierCosaEspecial | ;

CualquierCosa::= ExpresionGeneral {: pila.finExpresion(); :} |If | ForFact | While | Try | bbreak errEspecial |ccontinue errEspecial | rreturn Expresion 
| def errMalUbicacion dosPuntos | eexcept errMalUbicacion dosPuntos 
| var errMalUbicacion | clas errMalUbicacion puntoComa 
| ffinally errMalUbicacion dosPuntos
| eelse errMalUbicacion dosPuntos 
| elif errMalUbicacion dosPuntos;

CualquierCosaEspecial::= ExpresionGeneral |IfEspecial | ForFact | While | TryEspecial | bbreak|ccontinue | rreturn Expresion 
| def errMalUbicacion dosPuntos | eexcept errMalUbicacion dosPuntos 
| var errMalUbicacion | clas errMalUbicacion puntoComa 
| ffinally errMalUbicacion dosPuntos
| eelse errMalUbicacion dosPuntos 
| elif errMalUbicacion dosPuntos;  

/*----------------------------------------------------------------------------------------*/

VariablesOOP ::=  {: pila.ambitoActual = "ATRIBUTO"; :} Variable Variables;

FuncionesOOP ::=  DefFunciones2 puntoComa CodigoPrincipal 
| DefFunciones2:c {: errores.add("Falto el ; de la clase principal en la línea: " + cleft); :} CodigoPrincipal 
| puntoComa:c {: errores.add("debe venir al menos un método al definir la clase, en la línea:  " + cleft); :} CodigoPrincipal;

DefFunciones2 ::= DefFuncion DefFunciones2 
|  DefFuncion ;

FuncionesOOP2 ::= DefFunciones puntoComa CodigoPrincipal 
| DefFunciones:c {: errores.add("Falto el ; de la clase principal en la línea: " + cleft); :} CodigoPrincipal; 

OOP ::= HeaderOOP;
BloqueOOP::= VariablesOOP FuncionesOOP

| DefFuncion:c {: errores.add("debe venir al menos una variable al definir la clase, en la línea:  " + cleft); :} FuncionesOOP2
| puntoComa:c {: errores.add("debe venir al menos una variable y un método al definir la clase, en la línea:  " + cleft); :} CodigoPrincipal;

HeaderOOP ::= clas identificador dosPuntos BloqueOOP| clas errHeaderOOP BloqueOOP;


OpAritmeticos ::= Expresion:e1 opSuma:v Expresion:e2 {:pila.registrarOperador(v, "SUMA" ,vleft, vright); RESULT = new Nodo(e1,e2,v); pila.contadorExp+=1; :} 
| Expresion:e1 opResta:v Expresion:e2 {:pila.registrarOperador(v, "OPERADOR" ,vleft, vright); RESULT = new Nodo(e1,e2,v); pila.contadorExp+=1; :} 
| Expresion:e1 opMultiplicacion:v Expresion:e2 {:pila.registrarOperador(v, "OPERADOR" ,vleft, vright); RESULT = new Nodo(e1,e2,v); pila.contadorExp+=1; :}  
| Expresion:e1 opDivisionE:v Expresion:e2 {:pila.registrarOperador(v, "OPERADOR" ,vleft, vright); RESULT = new Nodo(e1,e2,v); pila.contadorExp+=1; :}  
| Expresion:e1 opDivision:v Expresion:e2 {:pila.registrarOperador(v, "OPERADOR" ,vleft, vright); RESULT = new Nodo(e1,e2,v); pila.contadorExp+=1; :}  
| Expresion:e1 opModulo:v Expresion:e2 {:pila.registrarOperador(v, "OPERADOR" ,vleft, vright); RESULT = new Nodo(e1,e2,v); pila.contadorExp+=1; :}  
| Expresion:e1 opPotencia:v Expresion:e2 {:pila.registrarOperador(v, "OPERADOR" ,vleft, vright); RESULT = new Nodo(e1,e2,v); pila.contadorExp+=1; :}  ;

OpUnario ::= opSuma:v {:pila.registrarOperador(v, "UNARIO" ,vleft, vright); :}   
| opResta:v {:pila.registrarOperador(v, "UNARIO" ,vleft, vright); :}  ;

OpBinario ::= OpAritmeticos;
OpBinarioB ::= opComGeneral:v {:pila.registrarOperador(v, "COMPGENERAL" ,vleft, vright); pila.contadorExp+=1; :}  
| opComEspecifico:v {:pila.registrarOperador(v, "COMPESPECIFICO" ,vleft, vright);pila.contadorExp+=1; :}    
| OpLogicos;

OpLogicos ::= opAnd:v {:pila.registrarOperador(v, "COMPGENERAL" ,vleft, vright);pila.contadorExp+=1; :}
| opOr:v {:pila.registrarOperador(v, "COMPGENERAL" ,vleft, vright);pila.contadorExp+=1; :};

OpBits ::= opShiftR:v {:pila.registrarOperadorBinario(v, "BINARIO" ,vleft, vright);pila.contadorExp+=1; :}
| opShiftL:v {:pila.registrarOperadorBinario(v, "BINARIO" ,vleft, vright);pila.contadorExp+=1; :}
| opAndBits:v {:pila.registrarOperadorBinario(v, "BINARIO" ,vleft, vright);pila.contadorExp+=1; :}
| opExclusiveOr:v {:pila.registrarOperadorBinario(v, "BINARIO" ,vleft, vright);pila.contadorExp+=1; :}
| opComp:v {:pila.registrarOperadorBinario(v, "BINARIO" ,vleft, vright);pila.contadorExp+=1; :}
| opOrBits:v {:pila.registrarOperadorBinario(v, "BINARIO" ,vleft, vright); pila.contadorExp+=1;:};

/*-----------------------------------------------------------------------------------------*/


/* --- Errores personalizados --- */
errHeaderOOP::= error:e       {: parser.addError("Hay un error en el header del fuente de tipo OOP."); :};
errDecVariables::= error:e    {: parser.addError("Hay un error al declarar variables. "); :};
errPuntoYComa::= error:e      {: parser.addError("Falto cerrar un bloque con ';'."); :};
errClase::= error:e           {: parser.addError("Hay un error en la clase."); :};
errExpresion::= error:e       {: parser.addError("Hay un error en la expresión. "); :};
errExpresionB::= error:e      {: parser.addError("Hay un error en la expresión. Debe ser booleana."); :};

errIf::= error:e              {: parser.addError("Hay un error en la definicion del if."); :};
errInput::= error:e           {: parser.addError("Hay un error en input()."); :};
errPrint::= error:e           {: parser.addError("Hay un error en print()"); :};
errDecFuncion::= error:e      {: parser.addError("Hay un error en la declaracion de la funcion. "); :};
errParametro1::= error:e      {: parser.addError("Hay un error en la declaracion de los parametros de la funcion. "); :};
errParametro::= error:e       {: parser.addError("Hay un error,falta la coma. "); :};
errBloque::= error:e          {: parser.addError("Hay un error Bloque. "); :};
errRange::= error:e           {: parser.addError("Hay un error en el range. "); :};
errIn::= error:e              {: parser.addError("Hay un error, falta 'in'. "); :};
errFor::= error:e             {: parser.addError("Hay un error en el for. "); :};
errElse::= error:e            {: parser.addError("Hay un error, faltan dos puntos en else."); :};
errDosPuntos ::= error:e      {: parser.addError("Hay un error, faltan los dos puntos."); :};

errTry ::= error:e                      {: parser.addError("Hay un error, en el try. Faltan dos puntos."); :};
errExcept ::= error:e                   {: parser.addError("Hay un error, except."); :};
errExcept2 ::= error:e                   {: parser.addError("Hay un error, No se leyo el except para el try."); :};
errFinally ::= error:e                  {: parser.addError("Hay un error, finally. Faltan dos puntos."); :};
errInt ::= error:e                  {: parser.addError("Hay un error, en la funcion Int."); :};
errParen ::= error:e                  {: parser.addError("Error parentesis."); :};
errMalUbicacion ::= error:e            {: parser.addError("Estructura invalida (verifique puntoYcoma de estructura anterior)."); :};
errEspecial ::= error:e            {: parser.addError("Break o Continue fuera de lugar."); :};